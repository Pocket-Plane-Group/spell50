BACKUP ~weidu_external/spell50/backup~
SUPPORT ~https://forums.pocketplane.net/index.php?board=43.0~
AUTO_EVAL_STRINGS

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @0 END // DLC Merger check

  ACTION_IF !VARIABLE_IS_SET cd_spell50 THEN BEGIN
  
    OUTER_SET cd_spell50 = 1
  
    INCLUDE ~spell50/lib/always.tpa~
  
    ACTION_IF enhanced_edition THEN BEGIN

      OUTER_SPRINT ~tra_location~ ~spell50/languages~
    
    END ELSE BEGIN
  
      OUTER_SPRINT ~tra_location~ ~weidu_external/spell50/languages~

      // convert strings from UTF-8 for originals
      ACTION_DEFINE_ARRAY cdreload BEGIN game END
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN weidu END
      LAF HANDLE_CHARSETS INT_VAR from_utf8 = 1 infer_charsets = 1 
                          STR_VAR default_language = ~english~ tra_path = ~spell50/languages~ out_path = ~weidu_external/spell50/languages~ noconvert_array = cdnoconvert reload_array = cdreload END
    
    END    

  END

END

VERSION ~v7~

README ~spell50/readme-spell50-%LANGUAGE%.html~ ~spell50/readme-spell50.html~

LANGUAGE ~English~ ~english~ 
  ~spell50/languages/english/weidu.tra~ // installer strings
  ~spell50/languages/english/game.tra~  // strings for the tlk

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Spell-50                                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Spell-50                                         \\\\\
/////                                                  \\\\\

BEGIN @2 DESIGNATED 0
SUBCOMPONENT @1 // extend spells
LABEL ~cd_spell-50~

OUTER_SET cap = 50
INCLUDE ~spell50/lib/spell50_core.tpa~

/////                                                  \\\\\
///// Spell-40                                         \\\\\
/////                                                  \\\\\

BEGIN @3 DESIGNATED 1
SUBCOMPONENT @1 // extend spells
LABEL ~cd_spell-40~

OUTER_SET cap = 40
INCLUDE ~spell50/lib/spell50_core.tpa~

/////                                                  \\\\\
///// Spell-30                                         \\\\\
/////                                                  \\\\\

BEGIN @4 DESIGNATED 2
SUBCOMPONENT @1 // extend spells
LABEL ~cd_spell-30~

OUTER_SET cap = 30
INCLUDE ~spell50/lib/spell50_core.tpa~

/////                                                  \\\\\
///// Spell-20                                         \\\\\
/////                                                  \\\\\

BEGIN @5 DESIGNATED 3
SUBCOMPONENT @1 // extend spells
LABEL ~cd_spell-20~

OUTER_SET cap = 20
INCLUDE ~spell50/lib/spell50_core.tpa~

/////                                                  \\\\\
///// Custom Cap                                       \\\\\
/////                                                  \\\\\

BEGIN @6 DESIGNATED 4
SUBCOMPONENT @1 // extend spells
REQUIRE_PREDICATE FILE_EXISTS ~spell50/custom_cap.txt~ @10
LABEL ~cd_spell-custom~

INCLUDE ~spell50/custom_cap.txt~

ACTION_IF !VARIABLE_IS_SET cap BEGIN
  ABORT @7
END ELSE BEGIN
  ACTION_IF !IS_AN_INT cap BEGIN
    ABORT @8
  END ELSE BEGIN
    ACTION_IF ((cap < 1) OR (cap > 50)) BEGIN
      ABORT @9
    END 
  END
END 

// only get here if cap is a number between 1 and 50
INCLUDE ~spell50/lib/spell50_core.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cam's info tool                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/*
this internal component is used by Cam to generate a list of spells 
with more than one ability. the generated list is then checked to see
if the spells should be extended. No tra references as it's purely a
component for Cam to use when making/updating the mod.
*/

BEGIN ~Cam's Info Tool~ DESIGNATED 10000
DEPRECATED ~Internal use only~

INCLUDE ~spell50/lib/info.tpa~
